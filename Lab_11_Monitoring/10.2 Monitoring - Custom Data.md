
# Tracking Custom Data
By default, a lot of runtime data is written to Application Insights, such as actions, triggers and traces. It is often useful however to write custom data that can be viewed and searched on. For example, in the workflows we have created, the request payload contains an order id - it would be useful to search on this in production to understand where a specific order may have failed.

## Tracked Properties
Azure Logic Apps supports the tracking of custom data through *Tracked Properties*. Tracked Properties are configured at the action level and can track one of three things:
- trigger body, or fields contained within the trigger body
- action inputs
- action outputs

This means that a single action is able to track the trigger body (and fields) and the inputs and outputs to that action.

### Track Order Details (from Trigger Body)
If we want to track some details such as the orderId and country the order originated, we can do this by adding the necessary configuration to *any* action. This is best configured on the first action in the workflow however as subsequent actions may fail, resulting in the tracked properties not being written.

Navigate to the *CreateOrder* workflow in Visual Studio Code and edit the *Response Variable* action, then navigate to *Settings* as follows:

![Tracked Properties](<images/Workflow - Tracked Properties Order Id.png>)

The Tracked Properties are entered in name value pairs - the value can be either a string value (i.e., hard coded) or an expression. We will enter two expressions to track the order id and country from the request. Use the following values for the Tracked Property settings:

- name *orderId*, value *@triggerBody().orderId*
- name *country*, value *@triggerBody().deliveryAddress.country*

Save the workflow and run it locally (ensure the workflow succeeds) then navigate to your Application Insights instance in Azure and navigate to *Logs* under *Monitoring* (you may need to enter into KQL mode rather than simple mode). Enter the following query:

``` SQL
requests
| where name == 'Response_Variable'
| order by timestamp desc 
```

In the results, navigate to *customDimensions* where you should see *trackedProperties*:

![Order Id](<images/Application Insights - Logs (order id).png>)

We can also query for a specific orderId:

``` SQL
requests
| where name == 'Response_Variable'
| extend trackedProps = todynamic(tostring(customDimensions.trackedProperties))
| extend orderId = tostring(trackedProps.orderId)
| where orderId == "55_1627"
| order by timestamp desc 
```
### Summarising Data

We can also summarise data. Use the following query to return all countries our orders are coming from (trigger your workflow multiple times and change the country):

``` SQL
requests
| where name == 'Response_Variable'
| extend trackedProps = todynamic(tostring(customDimensions.trackedProperties))
| extend orderId = tostring(trackedProps.orderId)
| extend country = tostring(trackedProps.country)
| where country != ''
| order by timestamp desc 

```
The country and orderId should both be visible in the results:
![Country](<images/Application Insights - Logs orderId and Country.png>)

We can also summarise the results to show orders by country:

``` SQL
requests
| where name == 'Response_Variable'
| extend trackedProps = todynamic(tostring(customDimensions.trackedProperties))
| extend orderId = tostring(trackedProps.orderId)
| extend country = tostring(trackedProps.country)
| where country != ''
| summarize Countries = count() by country
```

We can then render this as a pie chart by clicking *Chart*, or by rendering the result as a chart:
``` SQL
requests
| where name == 'Response_Variable'
| extend trackedProps = todynamic(tostring(customDimensions.trackedProperties))
| extend orderId = tostring(trackedProps.orderId)
| extend country = tostring(trackedProps.country)
| where country != ''
| summarize Countries = count() by country
| render piechart 
```

![Pie Chart](<images/Application Insights - Logs Piechart.png>)

## Joining Multiple Actions for End to End View

We can also create queries to report on the total time the integration took to execute from the start of the workflow to the end. For example, the following query will show when the order was created but crucially, when it completed and if it completed successfully. We will also retrieve the orderId.

``` SQL
requests
| extend orderReceived=timestamp
| where name == 'Order_Request'
| join kind=leftouter (
    requests
    | where name == 'Response'
    | extend orderCompleted=timestamp
    | extend success=success
  ) on operation_Id
| join kind=leftouter (
    requests
    | where name == 'Response_Variable'
    | extend trackedProps = todynamic(tostring(customDimensions.trackedProperties))
    | extend orderId = tostring(trackedProps.orderId)
  ) on operation_Id
| join kind=leftouter (
    requests
    | where name == 'Failure_Response'
    | extend trackedProps = todynamic(tostring(customDimensions.trackedProperties))
    | extend orderId = tostring(trackedProps.orderId)
  ) on operation_Id
| project orderId, orderReceived, orderCompleted,timestamp, success
| extend TotalCallTime = datetime_diff('second', orderCompleted, orderReceived)
| order by timestamp desc
```
This will return the following results:
![Log Output](<images/Application Insights - log output.png>)